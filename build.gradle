plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.example'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.60.0'
    implementation 'io.grpc:grpc-protobuf:1.60.0'
    implementation 'io.grpc:grpc-stub:1.60.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    runtimeOnly 'org.apache.tomcat:annotations-api:6.0.53'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.60.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

tasks.register('runServer', JavaExec) {
    mainClass = 'com.example.server.GrpcServer'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runClient', JavaExec) {
    mainClass = 'com.example.client.GrpcClient'
    classpath = sourceSets.main.runtimeClasspath
}